#include "tiny_rv.nh"
#include "fetch.nh"
#include "format.nh"
#include "integer_register.nh"
#include "integer_arithmetic_logic.nh"
#include "funct3.nh"

module tiny_rv
{
    reg pc[32];
    fetch ifetch;
    //decode idecode;
    integer_register reg_file;
    integer_arithmetic_logic ialu;

    reg fetch_0_inst[32], fetch_1_inst[32];
    proc_name fetch_0_stage(fetch_0_inst), fetch_1_stage(fetch_1_inst);
    reg exec_inst[32], exec_pc[32];
    proc_name execute_stage(exec_inst, exec_pc);
    reg wb_rd[5], wb_value[32], wb_pc[32], wb_opcode[7] = 0;
    reg wb_funct3[3];
    proc_name write_back_stage(wb_rd, wb_value, wb_opcode);
    proc_name write_back_stage_load(wb_rd, wb_value, wb_funct3);
    //proc_name write_back_stage_store(wb_rd, wb_value, wb_funct3);
    state_name load_store_idle, load_store_waiting;

    wire source_1[32];
    wire source_2[32];
    wire result[32];
    wire next_pc[32];
    wire comp_result;

    op_imm_t wire op_imm;
    op_t wire op;
    jal_t wire jal;
    jalr_t wire jalr;
    branch_t wire branch;
    load_t wire load;

    func_self stall();
    func_self interlock();

    func start
    {
        //fetch_0_stage(32'h00000000);
        fetch_0_stage(start_address);
    }

    func ifetch.request
    {
        request(ifetch.address);
    }

    func enable
    {
        ifetch.enable(instruction);
    }

    proc fetch_0_stage
    {
        next_pc = fetch_0_inst + 32'h4;
        if(!stall)
        {
            ifetch.run(fetch_0_inst);
        }
        if(ifetch.done)
        {
            execute_stage(ifetch.inst_out, fetch_0_inst);
            fetch_0_stage(next_pc);
        }
        else if(!stall)
        {
            fetch_1_stage(fetch_0_inst);
        }
    }

    proc fetch_1_stage
    {
        //TODO: when called stall
        if(ifetch.done)
        {
            execute_stage(ifetch.inst_out, fetch_1_inst);

            //XXX is not good
            fetch_0_stage(fetch_1_inst + 32'h4);
        }
    }

    proc execute_stage
    {
        any
        {
            exec_inst[6:0] == OP_IMM:
            {
                op_imm = exec_inst;
                source_1 = 32#op_imm.imm;
                //TODO: you have to check data hazard
                //when rs1 is equal to wb_rd, the value have to be bypassed.
                source_2 = reg_file.read_rs1(op_imm.rs1);
                result = ialu.calculate(
                        source_1, source_2,
                        op_imm.funct3, 1'b0);
                write_back_stage(op_imm.rd, result, op_imm.opcode);
            }
            exec_inst[6:0] == OP:
            {
                op = exec_inst;
                //TODO: you have to check data hazard
                //when rs1 or rs2 is equal to wb_rd,
                //          the value have to be bypassed.
                source_1 = reg_file.read_rs1(op.rs1);
                source_2 = reg_file.read_rs2(op.rs2);
                result = ialu.calculate(
                        source_1, source_2,
                        op.funct3, op.funct7[5]);
                write_back_stage(op.rd, result, op.opcode);
            }
            exec_inst[6:0] == JAL:
            {
                stall();
                jal = exec_inst;
                source_1 = 32#{jal.imm0, jal.imm1, jal.imm2, jal.imm3, 1'b0};
                source_2 = exec_pc;
                result = ialu.calculate(source_1, source_2, FUNCT3_ADD_SUB, 0);
                fetch_0_stage(result);
                write_back_stage(jal.rd, exec_pc, jal.opcode);
            }
            exec_inst[6:0] == JALR:
            {
                stall();
                jalr = exec_inst;
                source_1 = reg_file.read_rs1(jalr.rs1);
                source_2 = 32#jalr.imm;
                result = ialu.calculate(source_1, source_2, jalr.funct3, 0);
                fetch_0_stage(result);
                write_back_stage(jalr.rd, exec_pc, jalr.opcode);
            }
            exec_inst[6:0] == BRANCH:
            {
                branch = exec_inst;
                source_1 = reg_file.read_rs1(branch.rs1);
                source_2 = reg_file.read_rs2(branch.rs2);
                comp_result = ialu.compare(source_1, source_1, branch.opcode);
                result = ialu.calculate(
                            exec_pc,
                            32#{branch.imm12, branch.imm11, branch.imm10_5,
                                branch.imm4_1, 1'b0},
                            FUNCT3_ADD_SUB,
                            1'b0);
                if(comp_result)
                {
                    stall();
                    fetch_0_stage(result);
                }
            }
            exec_inst[6:0] == LOAD:
            {
                load = exec_inst;
                source_1 = reg_file.read_rs1(load.rs1);
                source_2 = 32#{load.imm};
                result = ialu.calculate(source_1, source_2,
                            FUNCT3_ADD_SUB, 1'b0);
                write_back_stage_load(load.rd, result, load.funct3);
            }
            //TODO add behavior of other opcodes
            else:
            {
                error();
            }
        }
        finish();
    }

    proc write_back_stage
    {
        any
        {
            wb_opcode == JAL || wb_opcode == JALR:
            {
                reg_file.write(wb_rd, wb_value);
            }
            wb_opcode == OP_IMM || wb_opcode == OP:
            {
                reg_file.write(wb_rd, wb_value);
            }
            else:
            {
                error();
            }
        }
        finish();
    }

    proc write_back_stage_load
    {
        state load_store_idle
        {
            any
            {
                wb_funct3 == WIDTH_B:
                {
                    load_b(wb_value);
                    goto load_store_waiting;
                    interlock();
                }
                else:
                {
                    error();
                }
            }
        }

        state load_store_waiting
        {
            if(load_done)
            {
                reg_file.write(wb_rd, load_data);
                finish();
                goto load_store_idle;
            }
            else
            {
                interlock();
            }
        }
    }
}
